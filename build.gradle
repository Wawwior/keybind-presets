plugins {
	id 'maven-publish'
	id "org.quiltmc.loom" version "1.2.+"
	id "com.modrinth.minotaur" version "2.+"
}

base.archivesName = archives_base_name
version = "$version+${minecraft_version}"
group = maven_group

repositories {
	maven {
		name = 'Wawwior'
		url = 'https://repo.wawwior.me/repository/maven-public/'
	}
	maven {
		name = 'Siphalor'
		url = 'https://maven.siphalor.de'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:intermediary-v2"

	modImplementation "org.quiltmc:quilt-loader:${loader_version}"

	modImplementation "org.quiltmc:qsl:${qsl_version}+${minecraft_version}"

	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilted_fabric_api_version}-${minecraft_version}"

	include(implementation('me.wawwior:config:1.3.8'))

	modCompileOnly("de.siphalor:amecsapi-1.20:1.5.1+mc1.20-pre1")

}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand 'version': version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Still required by IDEs such as Eclipse and Visual Studio Code
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	withSourcesJar()
}

jar {
	from('LICENSE') {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = 'Wfh4KfoK'
	uploadFile = remapJar
	gameVersions = ['1.20', '1.20.1']
	loaders = ['quilt']
	dependencies {
		optional.version "amecs", "1.3.9-mc.1.20.1"
	}

	syncBodyFrom = rootProject.file("README.md").text

}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

